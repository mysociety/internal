#!/usr/bin/perl -w
#
# from-mangle:
# Attempt to From_-mangle a mailbox which has been generated by some braindead
# piece of crap (e.g. Mailman) without proper From_-mangling.
#
# q.v. http://www.jwz.org/doc/content-length.html
#
# Copyright (c) 2006 UK Citizens Online Democracy. All rights reserved.
# Email: chris@mysociety.org; WWW: http://www.mysociety.org/
#

my $rcsid = ''; $rcsid .= '$Id: from-mangle,v 1.1 2006-07-08 18:48:22 chris Exp $';

use strict;

use IO::Handle;

sub next_line () {
    my $line = STDIN->getline();
    if (!defined($line)) {
        if (STDIN->error()) {
            die "standard input: $!";
        } else {
            return undef;
        }
    } else {
        return $line;
    }
}

my $n = 0;
my $last_line_blank = 0;
while (defined(my $line = next_line())) {
    ++$n;
    
    if ($last_line_blank && $line =~ /^From /) {
        my $from_ = $line;
        my $l = next_line();
        if (!$l) {
            # Last line of the folder, so escape it.
            print ">$from_";
        } elsif ($l =~ /^[a-z0-9]+(-[a-z0-9]+)*:\s/i) {
            # From_ line is followed by something which looks like an RFC822
            # header. So assume that it's a boundary.
            print "$from_$l";
        } else {
            # Not followed by a plausible header, so assume it's body text.
            print ">$from_$l";
        }
    } else {
        print $line;
    }

    if ($line =~ /^$/) {
        $last_line_blank = 1;
    } else {
        $last_line_blank = 0;
    }
}

